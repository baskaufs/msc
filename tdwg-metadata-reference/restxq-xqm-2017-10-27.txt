(: this module needs to be put in the webapp folder of your BaseX installation.  On my computer it's at c:\Program Files (x86)\BaseX\webapp\ :)

(: to test, send an HTTP GET to localhost:8984/Longxingsi using cURL, Postman, etc. :)

module namespace page = 'http://basex.org/modules/web-page';
import module namespace serialize = 'http://bioimages.vanderbilt.edu/xqm/serialize' at 'https://raw.githubusercontent.com/baskaufs/guid-o-matic/master/serialize.xqm';
(: import module namespace html = 'http://rs.tdwg.com/html' at 'https://raw.githubusercontent.com/tdwg/rs.tdwg.org/master/html/html.xqm'; :)
import module namespace html = 'http://rs.tdwg.com/html' at 'https://raw.githubusercontent.com/baskaufs/msc/master/tdwg-metadata-reference/html/html.xqm';

(:----------------------------------------------------------------------------------------------:)
(: Main functions for handling URI patterns :)

(: This is a temporary function for testing the kind of Accept header sent by the client :)
declare
  %rest:path("/header")
  %rest:header-param("Accept","{$acceptHeader}")
  function page:web($acceptHeader)
  {
  <p>{$acceptHeader}</p>
  };

(: This is a temporary function for testing the generation of an HTML page :)
declare
  %rest:path("/home")
  %rest:header-param("Accept","{$acceptHeader}")
  function page:home($acceptHeader)
  {
  <rest:response>
    <http:response status="200" message="Success">
      <http:header name="Content-Language" value="en"/>
      <http:header name="Content-Type" value="text/html; charset=utf-8"/>
    </http:response>
  </rest:response>,
  html:generate-list("terms")
  };

(: This is a hacked function to provice an option to dump the entire dataset :)
declare
  %rest:path("/dump/{$db}")
  %rest:header-param("Accept","{$acceptHeader}")
  function page:dump($acceptHeader,$db)
  {
    (: $db is the BaseX XML database to be dumped as RDF :)
  let $ext := page:determine-extension($acceptHeader)
  let $extension := 
      if ($ext = "htm")
      then 
          (: If the client is a browser, return Turtle :)
          "ttl"
      else
          (: Otherwise, return the requested media type :)
          $ext    
  let $response-media-type := page:determine-media-type($extension)
  let $flag := page:determine-type-flag($extension)

  return
      (
      <rest:response>
        <output:serialization-parameters>
          <output:media-type value='{$response-media-type}'/>
        </output:serialization-parameters>
      </rest:response>,
      serialize:main-db("",$flag,"dump",$db)
      )
  };

(: This is the main handler function for URI patterns with local names directly after the domain name :)
declare
  %rest:path("/{$full-local-id}")
  %rest:header-param("Accept","{$acceptHeader}")
  function page:content-negotiation($acceptHeader,$full-local-id)
  {
  let $db := "built-works" (: in this pattern-matching instance, the type of resource is described in the tang-song BaseX database.  :)
  let $lookup-string := $full-local-id
  return
    if (contains($full-local-id,"."))
    then page:handle-repesentation($acceptHeader,$full-local-id,$db,$lookup-string)
    else page:see-also($acceptHeader,$full-local-id,$db,$lookup-string)
  };

(: This is the handler function for URI patterns of "/{vocab}/{list}/" :)
declare
  %rest:path("/{$namespace}/{$local-id}")
  %rest:header-param("Accept","{$acceptHeader}")
  function page:content-negotiation-term-lists($acceptHeader,$namespace,$local-id)
  {
  let $db := "term-lists"
  return
    if (contains($local-id,"."))
    then
      (: has an extension :)
      let $stripped-local-name := substring-before($local-id,".")
      let $extension := substring-after($local-id,".")
      let $lookup-string := "http://rs.tdwg.org/"||$namespace||"/"||$stripped-local-name||"/"
      return page:handle-repesentation($acceptHeader,$extension,$db,$lookup-string)
    else 
      (: no extension :)
      let $lookup-string := "http://rs.tdwg.org/"||$namespace||"/"||$local-id||"/"
      let $redirect-id := "/"||$namespace||"/"||$local-id
      return page:see-also($acceptHeader,$redirect-id,$db,$lookup-string)
  };

(: This is the main handler function for URI patterns where the local name follows the "/{vocab}/version/{list}/" :)
declare
  %rest:path("/{$vocab}/version/{$term-list}/{$local-id}")
  %rest:header-param("Accept","{$acceptHeader}")
  function page:content-negotiation-term-lists-versions($acceptHeader,$vocab,$term-list,$local-id)
  {
  let $db := "term-lists-versions"
  return
    if (contains($local-id,"."))
    then
      (: has an extension :)
      let $stripped-local-name := substring-before($local-id,".")
      let $extension := substring-after($local-id,".")
      let $lookup-string := "http://rs.tdwg.org/"||$vocab||"/version/"||$term-list||"/"||$stripped-local-name
      return page:handle-repesentation($acceptHeader,$extension,$db,$lookup-string)
    else 
      (: no extension :)
      let $lookup-string := "http://rs.tdwg.org/"||$vocab||"/version/"||$term-list||"/"||$local-id
      let $redirect-id := $local-id
      return page:see-also($acceptHeader,$redirect-id,$db,$lookup-string)
  };

(: This is the main handler function for URI patterns where the local name follows the "/dwc/terms/" subpath :)
declare
  %rest:path("/dwc/terms/{$local-id}")
  %rest:header-param("Accept","{$acceptHeader}")
  function page:content-negotiation-terms($acceptHeader,$local-id)
  {
  let $db := "terms"
  return
    if (contains($local-id,"."))
    then 
      (: has an extension :)
      let $extension := substring-after($local-id,".")
      let $lookup-string := substring-before($local-id,".")
      return if ($lookup-string = "attributes")
             then
               (: handle the special case of the tdwgutility: namespace "/dwc/terms/attributes/". :)
               page:handle-repesentation($acceptHeader,$extension,"term-lists","http://rs.tdwg.org/dwc/terms/attributes/")
             else
               page:handle-repesentation($acceptHeader,$extension,$db,$lookup-string)
    else 
      (: no extension :)
      let $lookup-string := $local-id
      let $redirect-id := $local-id
      return if ($lookup-string = "attributes")
             then
               (: handle the special case of the tdwgutility: namespace "/dwc/terms/attributes/". :)
               page:see-also($acceptHeader,"/dwc/terms/attributes","term-lists","http://rs.tdwg.org/dwc/terms/attributes/")
             else
               page:see-also($acceptHeader,$redirect-id,$db,$lookup-string)
  };

(: This is the main handler function for URI patterns where the local name follows the "/dwc/iri/" subpath :)
declare
  %rest:path("/dwc/iri/{$local-id}")
  %rest:header-param("Accept","{$acceptHeader}")
  function page:content-negotiation-iri($acceptHeader,$local-id)
  {
  let $db := "iri"
  return page:generic-simple-id($local-id,$db,$acceptHeader)
  };

(: This is the main handler function for URI patterns where the local name follows the "/dwc/iri/versions/" subpath :)
declare
  %rest:path("/dwc/iri/versions/{$local-id}")
  %rest:header-param("Accept","{$acceptHeader}")
  function page:content-negotiation-iri-versions($acceptHeader,$local-id)
  {
  let $db := "iri-versions"
  return page:generic-simple-id($local-id,$db,$acceptHeader)
  };

(:----------------------------------------------------------------------------------------------:)
(: Second-level functions :)

(: Generic handler for simple local IDs :)
declare function page:generic-simple-id($local-id,$db,$acceptHeader)
{
    if (contains($local-id,"."))
    then 
      (: has an extension :)
      let $extension := substring-after($local-id,".")
      let $lookup-string := substring-before($local-id,".")
      return page:handle-repesentation($acceptHeader,$extension,$db,$lookup-string)
    else 
      (: no extension :)
      let $lookup-string := $local-id
      let $redirect-id := $local-id
      return page:see-also($acceptHeader,$redirect-id,$db,$lookup-string)
};

(: Handle request for specific representation when requested with file extension :)
declare function page:handle-repesentation($acceptHeader,$extension,$db,$lookup-string)
{
  if (serialize:find-db($lookup-string,$db))  (: check whether the resource is in the database :)
  then
    (: When a specific file extension is requested, override the requested content type. :)
    let $response-media-type := page:determine-media-type($extension)
    let $flag := page:determine-type-flag($extension)
    return page:return-representation($response-media-type,$lookup-string,$flag,$db)
  else
    page:not-found()  (: respond with 404 if not in database :)
};

(: Function to return a representation of a resource :)
declare function page:return-representation($response-media-type,$lookup-string,$flag,$db)
{
  if ($flag = "html")
  then page:handle-html($db,$response-media-type,$lookup-string)
  else   
  (: I moved this within the ELSE statement because it interferes with the HTML redirect if I leave it before the IF :)
  <rest:response>
    <output:serialization-parameters>
      <output:media-type value='{$response-media-type}'/>
    </output:serialization-parameters>
  </rest:response>,
  serialize:main-db($lookup-string,$flag,"single",$db)
};

(: Placeholder function to return a web page :)
declare function page:handle-html($db,$response-media-type,$lookup-string)
{
  <rest:response>
    <http:response status="200" message="Success">
      <http:header name="Content-Language" value="en"/>
      <http:header name="Content-Type" value="text/html; charset=utf-8"/>
    </http:response>
  </rest:response>,
  html:generate-list("terms")
  
  (: Perform a temporary redirect to the SPARQL-based web application that generates the HTML
  <rest:response>
    <http:response status="302">
      <http:header name="location" value="http://rs.tdwg.org/dwc/terms/#{$local-id}"/>
    </http:response>
  </rest:response>  :)
};

(: 303 See Also redirect to specific representation having file exension, based on requested media type :)
declare function page:see-also($acceptHeader,$redirect-id,$db,$lookup-string)
{
  if(serialize:find-db($lookup-string,$db))  (: check whether the resource is in the database :)
  then
      let $extension := page:determine-extension($acceptHeader)
      return
          <rest:response>
            <http:response status="303">
              <http:header name="location" value="{ concat($redirect-id,".",$extension) }"/>
            </http:response>
          </rest:response> 
  else
      page:not-found() (: respond with 404 if not in database :)
};

(: Function to generate a 404 Not found response :)
declare function page:not-found()
{
  <rest:response>
    <http:response status="404" message="Not found.">
      <http:header name="Content-Language" value="en"/>
      <http:header name="Content-Type" value="text/html; charset=utf-8"/>
    </http:response>
  </rest:response>
};

(:----------------------------------------------------------------------------------------------:)
(: Utility functions to set media type-dependent values :)

(: Functions used to set media type-specific values :)
declare function page:determine-extension($header)
{
  if (contains(string-join($header),"application/rdf+xml"))
  then "rdf"
  else
      if (contains(string-join($header),"text/turtle"))
      then "ttl"
      else 
          if (contains(string-join($header),"application/ld+json") or contains(string-join($header),"application/json"))
          then "json"
          else "htm" 
};

declare function page:determine-media-type($extension)
{
  switch($extension)
    case "rdf" return "application/rdf+xml"
    case "ttl" return "text/turtle"
    case "json" return "application/ld+json"
    default return "text/html"
};

declare function page:determine-type-flag($extension)
{
  switch($extension)
    case "rdf" return "xml"
    case "ttl" return "turtle"
    case "json" return "json"
    default return "html"
};










